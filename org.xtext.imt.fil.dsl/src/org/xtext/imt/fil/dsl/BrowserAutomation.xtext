grammar org.xtext.imt.fil.dsl.BrowserAutomation with org.eclipse.xtext.common.Terminals

generate browserAutomation "http://www.xtext.org/imt/fil/dsl/BrowserAutomation"

BrowserAutomation:
	{BrowserAutomation}
	'open' webBrowser=WebBrowser
	(statements+= Statement)*
;

WebBrowser:
	'firefox' | 'chrome' | 'safari' | 'internetExplorer'
;

Statement:
	DoAction | Get | UncheckAll | GoTo
;

DoAction:
	(get=Get)
	(action=Action)
;

Action:
	Click | Insert | Verify | Check | Choose | Contains
;

GoTo:
	'go' 'to' url=STRING
;

UncheckAll:
	'uncheck' 'all'
;

Get:
	varRef=Variable '=' 'get' (element=Element (order=Order)? ('by' attr=Attribut '[' attrVal=AttributeValue ']' | 'from' from=VarReference) | pageTitle='pageTitle')
;

AttributeValue:
	stringVal=STRING | varRefVal=VarReference
;

Order:
	'number' order=INT
;
 
Element:
	'link' | 'input' | 'button' | 'checkbox' | 'combobox' | 'image' | 'text' | 'title'
;

Attribut:
	'class' | 'value' | 'alt' | 'name' | 'href'
;

Variable:
	name=ID
;

VarReference:
	varRef=[Variable]
;

Click:
	'click' 'on' varRef=VarReference
;

Insert:
	'insert' (attrVal=STRING | varRef=VarReference) 'in' var2=VarReference
;

Verify:
	varToVerify=VarReference 'is' 'not' 'null'
;

Check:
	'check' varCheck=VarReference
;

Choose:
	'choose' value=STRING 'in' varChoose=VarReference
;

Contains:
	varContains=VarReference 'contains' varContained=VarReference
;